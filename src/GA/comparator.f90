MODULE comparator
    IMPLICIT NONE
    INTEGER, PARAMETER :: DP=KIND(1.0D0)
CONTAINS
    LOGICAL FUNCTION LOOK_ALIKE(NATOMS, COORDS1, COORDS2, PAIR_COR_CUM_DIFF, PAIR_COR_MAX)
        IMPLICIT NONE
        INTEGER, INTENT(IN) :: NATOMS
        REAL(KIND=DP), INTENT(IN), OPTIONAL :: PAIR_COR_CUM_DIFF, PAIR_COR_MAX
        REAL(KIND=DP), INTENT(IN), DIMENSION(3*NATOMS) :: COORDS1
        REAL(KIND=DP), INTENT(IN), DIMENSION(3*NATOMS) :: COORDS2
        REAL(KIND=DP), DIMENSION((NATOMS*(NATOMS-1))/2) :: DIST_LIST1, DIST_LIST2
        REAL(KIND=DP) :: CUM_DIFF, MAX_DIFF, DREL, DMAX

        CALL GET_SORTED_DIST_LIST(NATOMS, COORDS1, DIST_LIST1)
        CALL GET_SORTED_DIST_LIST(NATOMS, COORDS2, DIST_LIST2)
        IF(PRESENT(PAIR_COR_CUM_DIFF)) THEN
            DREL = PAIR_COR_CUM_DIFF
        ELSE
            DREL = 0.03D0   ! IN ANGSTROMS
        ENDIF
        IF(PRESENT(PAIR_COR_MAX)) THEN
            DMAX = PAIR_COR_MAX
        ELSE
            DMAX = 0.70D0   ! IN ANGSTROMS
        ENDIF

        CUM_DIFF = SUM(ABS(DIST_LIST1 - DIST_LIST2))/SUM(DIST_LIST1)
        MAX_DIFF = MAXVAL(ABS(DIST_LIST1 - DIST_LIST2))
        IF( CUM_DIFF < DREL .AND. MAX_DIFF < DMAX) THEN
            LOOK_ALIKE = .TRUE.
        ELSE
            LOOK_ALIKE = .FALSE.
        ENDIF
    END FUNCTION LOOK_ALIKE

    SUBROUTINE GET_SORTED_DIST_LIST(NATOMS, COORDS, DIST_LIST)
        IMPLICIT NONE
        INTEGER, INTENT(IN) :: NATOMS
        REAL(KIND=DP), DIMENSION(3*NATOMS) :: COORDS
        REAL(KIND=DP), DIMENSION((NATOMS*(NATOMS-1))/2) :: DIST_LIST
        INTEGER :: I, J, K
        K = 0
        DO I=1, NATOMS-1
            DO J=I+1, NATOMS
                K = K+1
                DIST_LIST(K) = NORM2(COORDS(3*I-2:3*I) - COORDS(3*J-2:3*J))
            ENDDO
        ENDDO
    END SUBROUTINE GET_SORTED_DIST_LIST

    SUBROUTINE CHECK_UNIQUENESS(ENERGY, UNIQUE_ENERGIES, COORDS, UNIQUE_COORDS,&
        ND, NUM_UNIQUE, DUPLICATE_INDX, ISUNIQUE, DUPLICATE_ETHRESH)
        IMPLICIT NONE
        INTEGER, INTENT(IN) :: ND, NUM_UNIQUE
        INTEGER, INTENT(OUT) :: DUPLICATE_INDX
        REAL(KIND=DP), INTENT(IN) :: ENERGY, DUPLICATE_ETHRESH
        REAL(KIND=DP), DIMENSION(ND) :: COORDS
        REAL(KIND=DP), DIMENSION(NUM_UNIQUE) :: UNIQUE_ENERGIES
        REAL(KIND=DP), DIMENSION(ND, NUM_UNIQUE) :: UNIQUE_COORDS
        INTEGER :: I,K
        LOGICAL, INTENT(OUT) ::  ISUNIQUE
        ! LOOP THROUGH STRUCTURES, STARTING WITH MOST STABLE
        DO I=1,NUM_UNIQUE
            IF(ABS(UNIQUE_ENERGIES(I) - ENERGY).LT.DUPLICATE_ETHRESH) THEN
                IF (LOOK_ALIKE(ND/3, COORDS, UNIQUE_COORDS(:,I))) THEN
                    ISUNIQUE=.FALSE. ! DUPLICATE FOUND
                    DUPLICATE_INDX = I
                ENDIF
                RETURN
            ENDIF
        ENDDO
        ISUNIQUE=.TRUE. !RUN OUT OF STRUCTURES, NO DUPLICATES FOUND
        DUPLICATE_INDX = 0
        RETURN
    END SUBROUTINE CHECK_UNIQUENESS

END MODULE comparator
